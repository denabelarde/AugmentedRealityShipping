apply plugin: 'com.android.application'
//apply plugin: 'com.google.firebase.firebase-perf'
apply from: '../gradle/dexcount.gradle'
apply from: '../gradle/jacoco.gradle'
apply from: '../gradle/quality.gradle'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools
    defaultConfig {
        applicationId "com.augmentedreality.simplus"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.code
        versionName versions.name
        multiDexEnabled false
        vectorDrawables.useSupportLibrary = true
        ndk {
            abiFilters "armeabi-v7a", "x86", "armeabi", "mips"
        }
        testInstrumentationRunner "com.augmentedreality.simplus.MockTestRunner"

        // puts the exported Room database schema into a directory
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions("generic")

    productFlavors {
        dev {
            versionNameSuffix "-DEV"
            buildConfigField "int", "DATABASE_VERSION", versions.database
            buildConfigField "String", "FIREBASE_URL", '"https://augmentedrealityshipping.firebaseio.com/"'
            dimension "generic"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        warningsAsErrors true
        abortOnError true

        // temporarily allowed until Butterknife becomes 9.0.0, when its
        // butterknife.lint.InvalidR2UsageDetector Lint bug is fixed
        disable 'ObsoleteLintCustomCheck'

        // disabled to allow MenuBuilder.setOptionalIconsVisible
        disable 'RestrictedApi'

        disable lintExemptions.googleAppIndexingWarning
        disable lintExemptions.logNotTimber
        disable lintExemptions.missingConstraints
        disable lintExemptions.invalidPackage
        disable lintExemptions.setTextI18n
        disable lintExemptions.allowBackUp
        disable lintExemptions.trulyRandom
        disable lintExemptions.vectorPath
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    sourceSets {
        test.java.srcDir 'src/tests/api/java'
        test.java.srcDir 'src/tests/commons/java'
        test.java.srcDir 'src/tests/unit/java'
        androidTest.java.srcDir 'src/tests/android/java'
        androidTest.java.srcDir 'src/tests/commons/java'

    }
    testOptions {
        execution 'ANDROID_TEST_ORCHESTRATOR'
    }
    testOptions.unitTests.all {
        testLogging {
            events 'failed'
            exceptionFormat

            // if not set, roboletric tests will not be included in coverage
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation libraries.dagger
    androidTestImplementation libraries.supportAnnotations
    androidTestImplementation libraries.dagger
    androidTestImplementation libraries.supportAnnotations
    androidTestImplementation libraries.supportAppCompat
    androidTestImplementation libraries.supportDesign
    androidTestImplementation libraries.supportRecyclerView
    annotationProcessor libraries.daggerCompiler
    annotationProcessor libraries.daggerProcessor
    implementation libraries.butterknife
    annotationProcessor libraries.butterknifeCompiler
    implementation libraries.supportV4
    implementation libraries.dagger
    implementation libraries.daggerAndroidSupport
    implementation libraries.dataFactory
    implementation libraries.gson
    implementation libraries.lifecycleCommons
    implementation libraries.lifecycleExtensions
    implementation libraries.mosbyMvp
    implementation libraries.okHttpLogging
    implementation libraries.retrofit
    implementation libraries.retrofitGsonConverter
    implementation libraries.retrofitRxJavaAdapter
    implementation libraries.room
    implementation libraries.roomRxJava2
    implementation libraries.rxAndroid
    implementation libraries.rxJava
    implementation libraries.supportAnnotations
    implementation libraries.supportAppCompat
    implementation libraries.supportCoreUtils
    implementation libraries.supportDesign
    implementation libraries.timber
    implementation libraries.sdp
    implementation 'com.google.android.gms:play-services-location:15.0.1'
    implementation libraries.firebaseCore
    implementation libraries.firebaseMessaging
    implementation libraries.firebaseAuth
    implementation libraries.firebaseStorage
    implementation libraries.firebaseUI
//    implementation libraries.firebasePerformance
    implementation libraries.firebaseDynamicLinks
    debugImplementation debugTools.leakCanary
    releaseImplementation debugTools.leakCanaryRelease
    implementation libraries.geofire
}

apply plugin: 'com.google.gms.google-services'
